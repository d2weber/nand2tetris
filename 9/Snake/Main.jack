class Main {
	function void main() {
		var GridP x, targ;
		var int i, direction; // 1=left, 2=up, 3=right, 4=down
		var int seed;
		var char key;
		var SnakeBody snake;

		do Output.moveCursor(14,19);
		do Output.printString("Press any key to begin.");
		let seed = 1;
		while (Keyboard.keyPressed() = 0) {let seed = seed + 1;}
		do Output.moveCursor(14,19);
		do Output.printString("                       ");

		do LCGRandom.setSeed(Math.abs(seed));

		let targ = GridP.new(
			LCGRandom.randRange(0,GridP.max_x()),
			LCGRandom.randRange(0,GridP.max_y())
		);

		let direction = 3;
		let snake = SnakeBody.new();

		do Output.moveCursor(0,0);
		do Output.printString("Score: ");
		do Output.printInt(snake.score());


		while (~snake.gameOver()) {
			if (targ.eq(snake.head())) {
				do Output.moveCursor(0,0);
				do Output.printString("Score: ");
				do Output.printInt(snake.score());

				do targ.disposeKeepDrawn();

				let targ = GridP.new(
					LCGRandom.randRange(0,GridP.max_x()),
					LCGRandom.randRange(0,GridP.max_y())
				);

				do snake.advanceGrowing(direction);
			} else {
				do snake.advance(direction);
			}

			let i = 0;
			while(i<30) {
				let i = i+1;

				let key = Keyboard.keyPressed();
		        if (key = 130) { let direction = 1; }   // left arrow
		        if (key = 131) { let direction = 2; }   // up arrow
		        if (key = 132) { let direction = 3; }   // right arrow
		        if (key = 133) { let direction = 4; }   // down arrow

				do Sys.wait(5);
			}
		}


		do Output.moveCursor(10, 28);
		do Output.printString("Game Over.");
		return;
	}
}
